#include <Servo.h>

// Arduino pin assignment
#define PIN_LED 9    // LED active-low
#define PIN_TRIG 12  // sonar sensor TRIGGER
#define PIN_ECHO 13  // sonar sensor ECHO
#define PIN_SERVO 10 // servo motor

// configurable parameters for sonar
#define SND_VEL 346.0     // sound velocity at 24 celsius degree (unit: m/sec)
#define INTERVAL 25       // sampling interval (unit: msec)
#define PULSE_DURATION 10 // ultra-sound Pulse Duration (unit: usec)
#define _DIST_MIN 180.0   // minimum distance to be measured (unit: mm)
#define _DIST_MAX 360.0   // maximum distance to be measured (unit: mm)

#define TIMEOUT                                                                \
  ((INTERVAL / 2) * 1000.0) // maximum echo waiting time (unit: usec)
#define SCALE                                                                  \
  (0.001 * 0.5 * SND_VEL) // coefficent to convert duration to distance

#define _EMA_ALPHA                                                             \
  0.3 // EMA weight of new sample (range: 0 to 1)
      // Setting EMA to 1 effectively disables EMA filter.

// Target Distance
#define _TARGET_LOW 250.0
#define _TARGET_HIGH 290.0

// duty duration for myservo.writeMicroseconds()
// NEEDS TUNING (servo by servo)

#define _DUTY_MIN 600 // servo full clockwise position (0 degree)
#define _DUTY_NEU ((_DUTY_MIN + _DUTY_MAX) / 2) // servo neutral position (90 degree)
#define _DUTY_MAX 2400 // servo full counterclockwise position (180 degree)

// global variables
float dist_ema, dist_prev = _DIST_MAX; // unit: mm
unsigned long last_sampling_time;      // unit: ms

Servo servo;

// get a distance reading from USS. return value is in millimeter.
float USS_measure(int TRIG, int ECHO) {
  digitalWrite(TRIG, HIGH);
  delayMicroseconds(PULSE_DURATION);
  digitalWrite(TRIG, LOW);

  return pulseIn(ECHO, HIGH, TIMEOUT) * SCALE; // unit: mm
}

void setup() {
  // initialize GPIO pins
  pinMode(PIN_LED, OUTPUT);
  pinMode(PIN_TRIG, OUTPUT);   // sonar TRIGGER
  pinMode(PIN_ECHO, INPUT);    // sonar ECHO
  digitalWrite(PIN_TRIG, LOW); // turn-off Sonar

  servo.attach(PIN_SERVO);
  servo.writeMicroseconds(_DUTY_NEU);

  // initialize USS related variables
  dist_prev = _DIST_MIN; // raw distance output from USS (unit: mm)
  dist_ema = _DIST_MIN;  // initial EMA value (unit: mm)

  // initialize serial port
  Serial.begin(57600);
}

void loop() {
  float dist_raw, dist_filtered;

  // wait until next sampling time.
  // millis() returns the number of milliseconds since the program started.
  // will overflow after 50 days.
  if (millis() < last_sampling_time + INTERVAL)
    return;

  // get a distance reading from the USS
  dist_raw = USS_measure(PIN_TRIG, PIN_ECHO);

  // the range filter
  digitalWrite(PIN_LED, HIGH);
  if ((dist_raw == 0.0) || (dist_raw > _DIST_MAX)) {
    dist_filtered = dist_prev;
  } else if (dist_raw < _DIST_MIN) {
    dist_filtered = dist_prev;
  } else { // In desired Range
    digitalWrite(PIN_LED, LOW);
    dist_filtered = dist_raw;
    dist_prev = dist_raw;
  }

  // EMA_k = alpha * d_k + (1 - alpha) * EMA_(k-1)
  dist_ema = _EMA_ALPHA * dist_filtered + (1 - _EMA_ALPHA) * dist_ema;

  // adjust servo position according to the USS read value
  if (dist_ema <= _TARGET_LOW) {
    servo.writeMicroseconds(_DUTY_MIN);
  } else if (dist_ema < _TARGET_HIGH) {
    servo.writeMicroseconds( map(dist_ema, _TARGET_LOW, _TARGET_HIGH, _DUTY_MIN, _DUTY_MAX));
  } else {
    servo.writeMicroseconds(_DUTY_MAX);
  }

  // output the distance to the serial port
  Serial.print("Min:");
  Serial.print(_DIST_MIN);
  Serial.print(",Low:");
  Serial.print(_TARGET_LOW);
  Serial.print(",dist:");
  Serial.print(dist_ema);
  Serial.print(",Servo:");
  Serial.print(servo.read());
  Serial.print(",High:");
  Serial.print(_TARGET_HIGH);
  Serial.print(",Max:");
  Serial.print(_DIST_MAX);
  Serial.println("");

  // update last sampling time
  last_sampling_time += INTERVAL;
}
